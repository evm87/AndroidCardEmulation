apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

//These variable refer to release builds so make sure they are correct. If you need to override them
//for some specific development needs then use variables that can be passed to gradle on command line.
String releaseVersionName = '0.0.1'
int releaseVersionCode = 1
int releaseMinSdk = 21

def sign

android {
    int developProductVersionCode = System.getenv("BUILD_ID") as Integer ?: releaseVersionCode
    String developProductVersionName = rootProject.hasProperty('productVersionName') ?
            rootProject.productVersionName : releaseVersionName
    int developMinSdk = rootProject.hasProperty('productMinSdk') ?
            rootProject.productMinSdk.toInteger() : releaseMinSdk

    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.evanmeyermann.cardemulationexample"
        minSdkVersion developMinSdk
        targetSdkVersion 28
        versionCode developProductVersionCode
        versionName developProductVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //Enable to support vector drawables
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }

    dataBinding {
        enabled = true
    }

    productFlavors {
        develop {
            applicationIdSuffix ".develop"
            versionNameSuffix "-develop"
            dimension "default"

        }
    }

    buildTypes {
        debug {
            dexOptions {
                jumboMode = true
                javaMaxHeapSize "1g"
            }
            minifyEnabled false

            //Used by crashlytics
            ext.alwaysUpdateBuildId = false

            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false

            if (sign != null) {
                signingConfig sign
            }
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            dexOptions {
                jumboMode = true
                javaMaxHeapSize "1g"
            }

            if (sign != null) {
                signingConfig sign
            }
        }
    }

    flavorDimensions "default"

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {

    def androidSupportVersion = "26.0.2"

    //"25.4.0"
    def constraint_layout_version = "1.0.2"
    def rxKotlin_version = "2.1.0"
    def rxAndroid_version = "2.0.2"
    def jUnitVersion = "4.12"

    //    def testRulesVersion = "1.0.0"
    def testRunnerVersion = "1.0.0"
    def moshiVersion = "1.5.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:multidex:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:support-v4:26.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:support-v13:$androidSupportVersion"
    implementation group: 'com.android.support.constraint', name: 'constraint-layout', version: "$constraint_layout_version"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:support-vector-drawable:$androidSupportVersion"
    implementation "com.android.support:animated-vector-drawable:$androidSupportVersion"

    //https://github.com/81813780/AVLoadingIndicatorView
    implementation 'com.wang.avi:library:2.1.3'

    //Thirty Inch
    def thirtyinchVersion = '0.9.0'

    // MVP for activity and fragment
    implementation "net.grandcentrix.thirtyinch:thirtyinch:$thirtyinchVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-vector-drawable'
        exclude group: 'com.android.support', module: 'animated-vector-drawable'
        exclude group: 'io.reactivex', module: 'rxjava'
    }
    implementation("net.grandcentrix.thirtyinch:thirtyinch-rx:$thirtyinchVersion") {
        exclude module: 'rxjava'
    }

    // rx extension
    implementation group: 'io.reactivex.rxjava2', name: 'rxandroid', version: "$rxAndroid_version"
    implementation group: 'io.reactivex.rxjava2', name: 'rxkotlin', version: "$rxKotlin_version"
    // JUnit4 Rules


    // Junit dependency for testing
    testImplementation "junit:junit:$jUnitVersion"

    //androidTestCompile "com.android.support.test:rules:$testRulesVersion"
    androidTestImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: "$kotlin_version"
    androidTestImplementation "com.android.support:support-annotations:$androidSupportVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$androidSupportVersion"
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.0.2'
            }
        }
    }
}

repositories {
    google()
}
